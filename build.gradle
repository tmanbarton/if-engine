plugins {
  id 'java-library'
  id 'maven-publish'
  id 'jacoco'
}

group = 'com.ifengine'
version = '1.0.0'

java {
  sourceCompatibility = '17'
  withSourcesJar()
}

tasks.withType(Javadoc).configureEach {
  options.addBooleanOption('Xdoclint:none', true)
  failOnError = false
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'com.google.code.findbugs:jsr305:3.0.2'

  testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
  testImplementation 'org.mockito:mockito-core:5.7.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:5.7.0'
  testImplementation 'org.json:json:20231013'
}

tasks.named('test') {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

jacoco {
  toolVersion = "0.8.12"
}

tasks.withType(Test) {
  jacoco {
    excludes = ['sun/**', 'jdk/**']
  }
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = false
    csv.required = false
    html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      pom {
        name = 'IF Engine'
        description = 'Interactive Fiction Game Engine'
      }
    }
  }
  repositories {
    mavenLocal()
  }
}